# Maintainer: envolution
# Contributor:  Luis Martinez <luis dot martinez at disroot dot org>
# Contributor:  Dimitris Kiziridis <ragouel at outlook dot com>
# Contributor: Ronuk Raval <ronuk.raval at gmail dot com>
# Contributor: Narrat <autumn-wind at web dot de>
# Contributor: David Scholl <djscholl at gmail dot com>
# shellcheck shell=bash disable=SC2034,SC2154
# ci|skipcheck|

pkgname=leo
pkgver=6.8.7
pkgrel=1
pkgdesc="Outliner, Editor, and Personal Information Manager"
arch=('any')
url='https://github.com/leo-editor/leo-editor'
license=('MIT' 'BSD')
depends=(
  'jupyter-nbformat'
  'python-asttokens'
  'python-black'
  'python-docutils'
  'python-flexx'
  'python-meta'
  'python-pyflakes'
  'python-pylint'
  'python-pyqt6'
  'python-pyqt6-webengine'
  'python-pyshortcuts'
  'python-sphinx'
  'shared-mime-info')
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel')
optdepends=('python-pyenchant: spellchecking support')
# checkdepends=('python-pytest')
provides=('leo-editor')
source=("$pkgname-$pkgver.tar.gz::https://files.pythonhosted.org/packages/source/l/leo/leo-${pkgver/_/-}.tar.gz"
  'leo.desktop'
  'leo.xml')
sha256sums=('2995e84e6c8f93f68e21193eb3ef8b75a27cd5b28a5e62b11555af2f9a0afd4a'
            '4633876eb91eff206660359ee7da459211e8f87fd73ebbc680fd437c70b63467'
            '630852279324b0d9acf656c4684f16777d64f49b4062bd101c5cddbfc33c82cb')

prepare() {
  cd "$pkgname-$pkgver"
  sed -i '/.....setuptools.*,$/d' pyproject.toml
}

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname-$pkgver"
  python -m pytest --ignore=leo/unittests/plugins/test_gui.py #this test fails without a screen set up
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir/" dist/*.whl
  install -Dvm644 "$srcdir/leo.desktop" -t "$pkgdir/usr/share/applications/"
  install -Dvm644 "$srcdir/leo.xml" -t "$pkgdir/usr/share/mime/packages/"
  install -Dvm644 "leo/Icons/application-x-leo-outline.png" "$pkgdir/usr/share/pixmaps/leo.png"
  local _site="$(python -c 'import site; print(site.getsitepackages()[0])')"
  install -dv "$pkgdir/usr/share/licenses/$pkgname/"
  ln -sv "$_site/leo-${pkgver%_*}.dist-info/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/"
}
# vim:set ts=2 sw=2 et:
