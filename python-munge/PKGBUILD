# Contributor: Carlos Aznar√°n <caznaranl@uni.pe>
# Contributor: Luis Martinez <luis dot martinez at disroot dot org>
# Contributor:	Dimitris Kiziridis <ragouel at outlook dot com>
_base=munge
pkgname=python-${_base}
pkgver=1.4.0
pkgrel=1
pkgdesc="Data manipulation client/library"
arch=(any)
url="https://github.com/20c/${_base}"
license=(Apache-2.0)
depends=(python-requests python-click)
makedepends=(python-build python-installer python-hatchling)
checkdepends=(python-pytest python-toml)
optdepends=('python-toml' 'python-tomlkit' 'python-yaml')
changelog=CHANGELOG.md
source=(${_base}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz)
sha512sums=('39b7bc6278a31edc70747deec157274754333bdbdde8d8cc90348b3ddb2e77af73f64e31139be9e5577f73a442097426ce393112a9a8b246faca75cf4a59409c')

build() {
  cd ${_base}-${pkgver}
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd ${_base}-${pkgver}
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest tests -k 'not toml_library'
}

package() {
  cd ${_base}-${pkgver}
  PYTHONPYCACHEPREFIX="${PWD}/.cache/cpython/" python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
