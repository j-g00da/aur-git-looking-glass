# Maintainer: Simon Brulhart <simon@brulhart.me>

pkgname=python-taxi
_name=taxi
pkgver=6.3.2
pkgrel=3
pkgdesc="Timesheeting tool that focuses on simplicity"
arch=('any')
license=('custom')
url="https://github.com/sephii/taxi"
depends=(python-appdirs python-click)
makedepends=(python-build python-flit-core python-installer python-wheel)
optdepends=("python-taxi-zebra: Zebra backend for Taxi")
source=(
    "$pkgname-$pkgver.tar.gz::https://github.com/sephii/taxi/archive/$pkgver.tar.gz"
    taxi-complete.bash
    taxi-complete.zsh
)
sha512sums=('54eb0282aeebd4d0925ac9de316ab42ddd6a246ec8d79f9f53eee7857718212b644a2ac0650bfba4e3ae90080792d44fcbef94b2874ad7ac6cb5b6d6bc740046'
            '006e058e78737f3e9da2da7a31e66b5b6b220c3a4c952cc911de8d093a13cb3df63cbf9a12fc4fb9a1b6a59031f5d44396871f49333956e24b9c58a487872152'
            '3bbb8f968dc1a6fabcc0b974441c59e09255b98706713b9c3737f985ca807ab41992885455e4afdfc8556e244f0abf6ebfb2dcd0ffbd657ed95100d85c9fba2b')

build() {
    cd $_name-$pkgver
    python -m build --wheel --no-isolation
}

package() {
    cd $_name-$pkgver
    python -m installer --destdir="$pkgdir" dist/*.whl

    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

    cd "$srcdir"
    # Completion file is generated by running `_TAXI_COMPLETE=bash_source taxi > taxi-complete.bash`
    install -Dm644 taxi-complete.bash "$pkgdir"/usr/share/bash-completion/completions/taxi
    # Completion file is generated by running `_TAXI_COMPLETE=zsh_source taxi > taxi-complete.zsh`
    # and adding `#compdef taxi` on the first line
    install -Dm644 taxi-complete.zsh "$pkgdir"/usr/share/zsh/site-functions/_taxi
}
