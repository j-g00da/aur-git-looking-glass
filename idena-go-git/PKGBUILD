# Maintainer: Chris Sculley <cs.sculley at gmail dot com>
pkgname=idena-go-git
pkgver=v0.4.0.r258.gab3ae34
pkgrel=1
pkgdesc="The idena-go node for the Idena network"
arch=("x86_64")
url="https://github.com/idena-network/idena-go"
license=("GPL3")
makedepends=("git" "go-pie")
depends=("glibc")
provides=("idena-go")
conflicts=("idena-go")
backup=("etc/idena-go/idena-go.json")
source=("$pkgname::git+https://github.com/idena-network/idena-go.git"
        "idena-go.json"
        "idena-go.service")
md5sums=("SKIP"
        "ae54e697db92d52fccd45508257daf16"
        "6001d4aab3b42844f2c087e06c9e4598")

pkgver() {
    cd "$srcdir/$pkgname"
    ( set -o pipefail
        git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

build() {
    cd "$srcdir/$pkgname"
    _version=`git describe --abbrev=0 --tags | sed -e "s/v//g"`
    echo Building Version: $_version
    go build \
        -trimpath \
        -ldflags "-extldflags $LDFLAGS -X main.version=$_version" \
        -o idena-go .
}

package() {
    cd "$srcdir/$pkgname"
    
    mkdir -m755 "$pkgdir"/{usr,usr/bin,etc,etc/idena-go,usr/lib,usr/lib/systemd,usr/lib/systemd/system} 
    cp "$srcdir"/idena-go.json "$pkgdir"/etc/idena-go/idena-go.json
    cp "$srcdir"/idena-go.service "$pkgdir"/usr/lib/systemd/system/idena-go.service
    
    install -Dm755 idena-go -t "$pkgdir"/usr/bin/
    # install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}
 
