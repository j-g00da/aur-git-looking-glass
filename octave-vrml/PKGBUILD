#
# Maintainer: Carlos Aznar√°n <caznaranl@uni.pe>
# Contributor: wangjiezhe <wangjiezhe@gmail.com>
# Contributor: Clemens Buchacher <drizzd@aon.at>
#
# You can use the newpkg script from
# https://github.com/drizzd/octave-forge-archlinux to automatically generate
# new octave-forge PKGBUILDs or update existing ones. Patches welcome.
#

_pack=vrml
pkgname=octave-${_pack}
pkgver=1.0.14
pkgrel=3
pkgdesc="3D graphics using VRML"
arch=(any)
url="https://gnu-octave.github.io/packages/${_pack}"
license=('custom')
groups=('octave-forge')
depends=('octave>=2.9.7' 'octave-linear-algebra' 'octave-miscellaneous' 'octave-struct' 'octave-statistics' 'castle-model-viewer')
makedepends=()
optdepends=()
backup=()
options=()
install=${pkgname}.install
_archive=${_pack}-${pkgver}.tar.gz
source=("https://downloads.sourceforge.net/octave/${_archive}")
noextract=("${_archive}")
sha512sums=('5d9c67661afdf1a7bcb71563318492aa719d1951c9c305efda56430c513361cd3f8f6ad0dc8846a595b88ec73e98809e050382f3df9b5a4f6e0548cd35fd35c8')

_octave_run() {
  octave --no-history --no-init-file --no-window-system -q -f --eval "$*"
}

_install_dir() {
  src=$1
  dst=$2
  mkdir -p "$(dirname "$dst")"
  cp -rT "$src" "$dst"
}

build() {
  _prefix="$srcdir"/install_prefix
  _archprefix="$srcdir"/install_archprefix
  mkdir -p "$_prefix" "$_archprefix"
  cd "$srcdir"
  _octave_run "$(
    cat <<-EOF
			pkg local_list octave_packages;
			pkg prefix $_prefix $_archprefix;
			pkg install -verbose -nodeps $_archive;
		EOF
  )"
}

package() {
  prefix=$pkgdir/usr/share/octave/packages
  archprefix=$pkgdir/usr/lib/octave/packages
  _install_dir "$srcdir"/install_prefix "$prefix"
  _install_dir "$srcdir"/install_archprefix "$archprefix"
}
