# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=kickpad
pkgver=1.5.4
pkgrel=1
pkgdesc='Kick drum audio sample generator'
arch=(x86_64)
url='https://github.com/xyproto/kickpad'
license=(MIT)
depends=(glfw sdl2 sdl2_mixer)
makedepends=(gendesk git go)
source=("git+$url#tag=v$pkgver")
b2sums=('5ef238c91b6d6879387cea7e5e358e9f810116ee45686e35079644385c975c5c66f9642b17eeab039bb1e7c2d40a3ba481f4da0406f8ccd9344fbd355bc5cb19')

prepare() {
  gendesk -f -n --pkgdesc="$pkgdesc" --pkgname=$pkgname --categories 'AudioVideo;Audio'
}

build() {
  cd $pkgname
  go build -tags=enable_cimgui_sdl2 -mod=mod -v -trimpath -buildmode=pie -ldflags="-s -w -extldflags '$LDFLAGS'"
}

package() {
  install -Dm755 -t "$pkgdir/usr/bin" $pkgname/$pkgname
  install -Dm644 -t "$pkgdir/usr/share/applications" $pkgname.desktop
  install -Dm644 $pkgname/img/icon.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm755 -t "$pkgdir/usr/share/licenses/$pkgname" $pkgname/LICENSE
}
