# Maintainer: ninov <dev@ninov.de>
pkgname="brother-mfc-5895cw"
pkgver="1.0.0"
pkgrel=1
epoch=1

pkgverlpr=1.1.3
pkgrellpr=4
pkgvercw=1.1.3
pkgrelcw=6

pkgdesc="LPR and CUPS driver for the Brother MFC-5895CW, aur package based on the brother-mfc-5890cn package by Morn"
arch=("i686" "x86_64")
url="http://solutions.brother.com/linux/en_us/"
license=('custom:brother commercial license')
depends=("cups")
install="$pkgname.install"
source=(
	"http://www.brother.com/pub/bsc/linux/dlf/mfc5895cwlpr-$pkgverlpr-$pkgrellpr.i386.rpm"
	"http://www.brother.com/pub/bsc/linux/dlf/mfc5895cwcupswrapper-$pkgvercw-$pkgrelcw.i386.rpm"
	'cupswrapper-license.txt'
	'lpr-license.txt'
)
md5sums=(
	'b31842e87b36fa70842cb55920dce0e0'
	'723b0b266fa920830dcaf50e4da0e2c8'
	'97ad0cffd216059e9d1d3121899d8646'
	'aa36745fb35ddff6343bed3ae8dae5eb'
)
if test "$CARCH" == x86_64; then
  depends+=('lib32-glibc')
fi

optdepends=(
	'brscan3: scanning support'
	'brscan-skey: scankey support'
)	

prepare() {
#  do not install in '/usr/local'
	if [ -d $srcdir/usr/local/Brother ]; then
		install -d $srcdir/usr/share
		mv $srcdir/usr/local/Brother/ $srcdir/usr/share/brother
		rm -rf $srcdir/usr/local
	        sed -i 's|/usr/local/Brother|/usr/share/brother|g' `grep -lr '/usr/local/Brother' ./`
        fi
# setup cups-directories
	install -d $srcdir/usr/share/cups/model
	install -d $srcdir/usr/lib/cups/filter
#  go to the cupswrapper directory and find the source file from wich to generate a ppd- and wrapper-file
	cd `find . -type d -name 'cupswrapper'`
	if [ -f cupswrapper* ]; then
		_wrapper_source=`ls cupswrapper*`
		sed -i '/^\/etc\/init.d\/cups/d' $_wrapper_source
		sed -i '/^sleep/d' $_wrapper_source
		sed -i '/^lpadmin/d' $_wrapper_source
		sed -i 's|/usr|$srcdir/usr|g' $_wrapper_source
		sed -i 's|/opt|$srcdir/opt|g' $_wrapper_source
		sed -i 's|/model/Brother|/model|g' $_wrapper_source
		sed -i 's|lpinfo|echo|g' $_wrapper_source
		export srcdir=$srcdir
		./$_wrapper_source
		sed -i 's|$srcdir||' $srcdir/usr/lib/cups/filter/*lpdwrapper*
		sed -i "s|$srcdir||" $srcdir/usr/lib/cups/filter/*lpdwrapper*
		rm $_wrapper_source
	fi
#  /etc/printcap is managed by cups
	rm `find $srcdir -type f -name 'setupPrintcap*'`
}
package() {
	cp -R $srcdir/usr $pkgdir
	if [ -d $srcdir/opt ]; then cp -R $srcdir/opt $pkgdir; fi
	install -m 644 -D cupswrapper-license.txt $pkgdir/usr/share/licenses/${pkgname}/cupswrapper-licence.txt
	install -m 644 -D lpr-license.txt $pkgdir/usr/share/licenses/${pkgname}/lpr-licence.txt
}

