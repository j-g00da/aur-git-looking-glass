# Maintainer: clove3am (aka: Caltlgin) <clove.dev.mailbox.org>

pkgname='gopher2600'
pkgver=0.50.1
pkgrel=1
pkgdesc='Emulator for the Atari 2600 games console'
arch=('x86_64')
url='https://github.com/JetSetIlly/Gopher2600'
license=('GPL-3.0-or-later')
depends=(
  'gcc-libs'
  'glibc'
  'libgl'
  'sdl2'
)
makedepends=('go')
source=(
  "${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
  'gopher2600.desktop'
  'icon256.png'
)
sha256sums=(
  '99e1a689f13bf820eb10a57d846c0ae86f3c332aa21da8ade8ab6fd4e0fcd909'
  '5a7e3c60624007a1eb9d126097590ff869fdf66512348c5938cb53a1cccd353d'
  'a5aa462653391b3861d1c50181526830903e5c32414b62aa33ddc5675a7d713c'
)

build() {
  cd "${pkgname^}-${pkgver}"
  make release
}

package() {
  cd "${pkgname^}-${pkgver}"
  install -Dm755 "${pkgname}_linux_amd64" "${pkgdir}"/usr/bin/"${pkgname}"
  install -Dm644 'README.md' -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 "../gopher2600.desktop" -t "${pkgdir}/usr/share/applications"
  install -Dm644 "../icon256.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}

