# Maintainer: Kevin Meagher <kmeagher at icecube dot wisc dot edu>
pkgname=healpix
pkgver='3.83'
_pkgdate='2024Nov13'
pkgrel=0
pkgdesc="Software for pixelization, hierarchical indexation, synthesis, analysis, and visualization of data on the sphere."
arch=('x86_64')
url="https://healpix.jpl.nasa.gov/"
license=('GPL-2.0-or-later')
groups=()
depends=(glibc gcc-libs cfitsio)
makedepends=()
provides=()
conflicts=(libsharp)
replaces=()
backup=()
options=(!emptydirs)
install=
source=("https://downloads.sourceforge.net/project/healpix/Healpix_${pkgver}/Healpix_${pkgver}_${_pkgdate}.tar.gz")
md5sums=('8eb74bc9b002c8c821dd42499138111b')
sha1sums=('afb8874eb580ae8d1b320a244bbcc7c19cd8db7b')
sha256sums=('8876c18efc596fd706b2a004ac15f2fb60b795f2db6fbabea9d8ccf549531dda')

build() {
    cd "${srcdir}/Healpix_${pkgver}/src/C/autotools"
    autoreconf --install
    ./configure --prefix=/usr
    make -j

    cd "${srcdir}/Healpix_${pkgver}/src/common_libraries/libsharp"
    ./configure --prefix=/usr
    make -j

    cd "${srcdir}/Healpix_${pkgver}/src/cxx"
    autoreconf --install
    export SHARP_CFLAGS="-I${srcdir}/Healpix_${pkgver}/src/common_libraries/libsharp/"
    export SHARP_LIBS="-L${srcdir}/Healpix_${pkgver}/src/common_libraries/libsharp/.libs -lsharp"
    ./configure --prefix=/usr
    make -j
}

package() {
    cd "${srcdir}/Healpix_${pkgver}/src/C/autotools"
    make DESTDIR=${pkgdir} install

    cd "${srcdir}/Healpix_${pkgver}/src/common_libraries/libsharp"
    make DESTDIR=${pkgdir} install

    cd "${srcdir}/Healpix_${pkgver}/src/cxx"
    make DESTDIR=${pkgdir} install
}

