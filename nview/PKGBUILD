# Maintainer: Rafael Dominiquini <rafaeldominiquini at gmail dot com>

_pkgauthor=d99kris
_pkgname=nview
pkgname=${_pkgname}
pkgver=0.30
pkgrel=1
_pkgvername=v${pkgver}
pkgdesc="Minimalistic image viewer"
arch=('x86_64')
url="https://github.com/${_pkgauthor}/${_pkgname}"
_urlraw="https://raw.githubusercontent.com/${_pkgauthor}/${_pkgname}/${_pkgvername}"
license=('GPL-2.0')

provides=("${_pkgname}")
conflicts=("${_pkgname}")
depends=('gcc-libs' 'glibc' 'qt6-base' 'libglvnd' 'imagemagick')
makedepends=('qt6-base' 'qt6-tools' 'qtutilities-qt6' 'qt6-multimedia' 'qt6-multimedia-ffmpeg' 'qt6-imageformats' 'help2man')

source=("https://github.com/${_pkgauthor}/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('05fefaa13f8a7c12408a4f38ab869e7b80ab351214b7c615d02e66c80c0ed66a')


build() {
	cd "${srcdir}/${pkgname}-${pkgver}" || exit

	mkdir -p build && cd build

	qmake6 .. && make

	help2man -n "nano view" -N -o "../res/${_pkgname}.1" "./${_pkgname}" && sed -i "s/\.\\\\\" DO NOT MODIFY THIS FILE\!  It was generated by help2man.*/\.\\\\\" DO NOT MODIFY THIS FILE\!  It was generated by help2man./g" "../res/${_pkgname}.1" || exiterr "doc failed, exiting."
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}" || exit

	install -Dm755 "build/${_pkgname}" "${pkgdir}/usr/bin/${pkgname}"

	install -Dm644 "res/${_pkgname}.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"

	install -Dm644 "README.md" "${pkgdir}/usr/share/doc/${pkgname}/README.md"

	install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
