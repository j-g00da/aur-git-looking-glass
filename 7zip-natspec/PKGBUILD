# Maintainer: Taiki Sugawara <buzz.taiki@gmail.com>
# Contributor: Evangelos Foutras <foutrelis@archlinux.org>

pkgname=7zip-natspec
_pkgname=7zip
pkgver=25.01
pkgrel=2
pkgdesc="File archiver for extremely high compression, using libnatspec patch"
arch=('x86_64')
url="https://www.7-zip.org"
license=('LGPL-2.1-or-later' 'BSD-3-Clause' 'LicenseRef-UnRAR')
depends=('glibc' 'gcc-libs' 'sh' 'libnatspec')
makedepends=('uasm')
provides=('p7zip' '7zip')
conflicts=('p7zip' '7zip')
source=(https://7-zip.org/a/7z${pkgver//./}-src.tar.xz
       natspec.patch)
sha256sums=('ed087f83ee789c1ea5f39c464c55a5c9d4008deb0efe900814f2df262b82c36e'
            '594bb07a6d35b55e04f9bc4c45c4cebdf497dd4d960e02f440552d2d6df7ec99')


prepare() {
  find . -type f -print0 | xargs -0 chmod 644
  patch -p1 < ../natspec.patch
}

build() {
  local _platform_flags=()

  case $CARCH in
    x86_64)
      _platform_flags=(PLATFORM=x64 IS_X64=1 MY_ASM=uasm USE_ASM=1)
      ;;
    i686)
      _platform_flags=(PLATFORM=x86 IS_X86=1 MY_ASM=uasm USE_ASM=1)
      ;;
    aarch64)
      _platform_flags=(PLATFORM=arm64 IS_ARM64=1 MY_ASM=uasm USE_ASM=1)
      ;;
  esac

  for component in Bundles/{Alone,Alone7z,Format7zF,SFXCon} UI/Console; do
    make -C CPP/7zip/$component -f ../../cmpl_gcc.mak "${_platform_flags[@]}" \
      LFLAGS_STRIP= \
      CC="cc $CPPFLAGS $CFLAGS $LDFLAGS" \
      CXX="g++ $CPPFLAGS $CXXFLAGS $LDFLAGS"
  done
}

package() {
  install -Dt "$pkgdir/usr/lib/7zip" \
    CPP/7zip/Bundles/Alone/b/g/7za \
    CPP/7zip/Bundles/Alone7z/b/g/7zr \
    CPP/7zip/Bundles/Format7zF/b/g/7z.so \
    CPP/7zip/UI/Console/b/g/7z
  install -D CPP/7zip/Bundles/SFXCon/b/g/7zCon "$pkgdir/usr/lib/7zip/7zCon.sfx"

  for _prog in 7za 7zr 7z; do
    printf '#!/bin/sh\nexec /usr/lib/7zip/%s "$@"\n' "$_prog" \
    | install -D /dev/stdin "$pkgdir/usr/bin/$_prog"
  done

  install -Dm644 -t "$pkgdir/usr/share/licenses/$_pkgname" DOC/{,unRar}License.txt
}

# vim:set ts=2 sw=2 et:
