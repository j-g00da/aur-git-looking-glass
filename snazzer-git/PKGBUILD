# Maintainer: Jonne Ha√ü <me@jhass.eu>
pkgname=snazzer-git
pkgver=r226.50bf4f2
pkgrel=1
pkgdesc="btrfs snapshotting and backup system offering snapshot measurement, transport and pruning. "
arch=('any')
url="https://github.com/csirac2/snazzer"
license=('BSD')
depends=('btrfs-progs')
makedepends=('git')
source=("git+https://github.com/csirac2/snazzer.git")
sha256sums=('SKIP')

pkgver() {
  cd "snazzer"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

# Requires sudo currently
#check() {
#  cd "$srcdir/snazzer"
#
#  make test
#}

prepare() {
    cd "$srcdir/snazzer"
    sed -i '/export PATH/s#$#:/usr/bin/core_perl#' snazzer-send-wrapper
}

package() {
  cd "$srcdir/snazzer"

  make INSTALL_PREFIX="$pkgdir/usr/" install
  install -Dm644 "LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}
