# Maintainer: TÃ©rcio Martins <echo dGVyY2lvd2VuZGVsQGdtYWlsLmNvbQo= | base64 -d>

pkgname=r3d-sdk
pkgver=9.0.0
pkgrel=1
arch=('x86_64')
pkgdesc="SDK to load and decode R3D files generated by RED cameras"
url="https://www.red.com/developers"
license=('LicenseRef-EULA')
depends=('gcc-libs' 'util-linux-libs')
makedepends=('poppler')
options=('!strip')
source=("https://downloads.red.com/software/sdk/R3D/${pkgver%.*}/R3DSDKv${pkgver//./_}.zip")
b2sums=('f84b83ee38af110f40311b8837acd60a02b51b8d712be7df5764178874417932bb6720850fe72eabdd4a62baa9826036f4cc5150a89042c4b926bb5551e08278')

prepare(){
  pdftotext -layout "R3DSDKv${pkgver//./_}/SDK License Agreement.pdf" 2>&-
  mv "R3DSDKv${pkgver//./_}/SDK License Agreement.txt" "$srcdir"
}

package() {
  install -d "$pkgdir/opt"
  install -d "$pkgdir/usr/share/licenses/$pkgname"

  cp -dr --no-preserve=ownership "R3DSDKv${pkgver//./_}" \
                                 "$pkgdir/opt/R3DSDK"

  cp --no-preserve=ownership "$srcdir/SDK License Agreement.txt" \
                             "$pkgdir/usr/share/licenses/$pkgname"
}
