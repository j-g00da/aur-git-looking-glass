# Maintainer: graysky <therealgraysky AT protonmail DOT com>
# Contributer: Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Contributor: yugrotavele <yugrotavele at archlinux dot us>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Contributor: Arnaud Durand <biginozNOSPAM_at_free.dot.fr>
# Contributor: Nuno Araujo <nuno.araujo at russo79.com>

pkgname=bleachbit-cli
_pkgname=bleachbit
pkgver=5.0.0
pkgrel=1
pkgdesc='Deletes unneeded files to free disk space and maintain privacy. CLI version/no GUI.'
conflicts=($_pkgname)
provides=("bleachbit=${pkgver}")
arch=(any)
url='https://www.bleachbit.org/'
license=(GPL-3.0-or-later)
depends=(python-gobject python-psutil)
makedepends=(git)
source=(git+https://github.com/bleachbit/bleachbit.git#tag=v${pkgver}?signed)
validpgpkeys=(A9E582E4054A159315EDC943D6D447B02B4D4C9D) # Andrew Ziem <ahz001@gmail.com>
sha256sums=('64bdd50935d341aaa12c07cbf546ce530f4f5c20ca8e48bc0c7a8ad91986d05b')

package() {
  cd $_pkgname

  make prefix=/usr DESTDIR="$pkgdir" install
  # to avoid cache getting generated by executable pre-build it which
  # makes for a clean uninstall/update
  python -m compileall -d /usr "$pkgdir"/usr
  python -O -m compileall -d /usr "$pkgdir"/usr

  # suggestion by Meow to force script to operate in cli mode
  sed -i 's/if 1/if 0/' "$pkgdir"/usr/bin/bleachbit

  rm -rf "$pkgdir"/usr/share/{applications,pixmaps}
  rm -rf "$pkgdir"/usr/share/polkit-1
}
