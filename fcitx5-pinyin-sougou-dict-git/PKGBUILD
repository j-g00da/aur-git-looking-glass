# Contributor: Myt1 <MYT1 @ QQ.com>

pkgname=fcitx5-pinyin-sougou-dict-git
_pkgname=sougouscel
pkgver=20250802.r1.gbaf36c7
pkgrel=1
pkgdesc="Fcitx5-pinyin搜狗官网词库全部词量"
arch=('any')
url="https://pinyin.sogou.com/dict/"
license=('custom')
makedepends=('libime' 'git')
source=(git+https://github.com/maoyaotang12/sougouscel.git)
sha512sums=('SKIP')

pkgver() {
    cd ${_pkgname}
    git describe --long --tags --abbrev=7 | sed 's/^v//;s/-/.r/;s/-/./'
}

prepare(){
    cd ${_pkgname}/scel
    chmod +x txt.sh
    ./txt.sh
    rm -r txt.sh
}

build() {
	cat ${_pkgname}/scel/*.txt > sougouciku.txt
	libime_pinyindict sougouciku.txt sougouciku.dict
}

package() {
	install -Dm644 sougouciku.dict -t $pkgdir/usr/share/fcitx5/pinyin/dictionaries/
}
