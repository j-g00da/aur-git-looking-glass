# Maintainer: Hector Mtz-Seara (hseara#at#gmail#com)
# Maintainer: Vedran Miletic <vedran AT miletic DOT net>

pkgname=python-mdanalysis
pkgver=2.9.0
pkgrel=1
pkgdesc="An object-oriented python toolkit to analyze molecular dynamics trajectories generated by CHARMM, Gromacs, NAMD, LAMMPS, or Amber."
url="http://www.mdanalysis.org/"
license=('LGPL-3.0-or-later')
arch=('x86_64')
depends=('python-numpy' 'cython' 'python-mmtf' 'python-gsd' 'python-tqdm' 'python-griddataformats' 'python-scipy' 'python-biopython' 'python-fasteners' 'python-packaging' 'python-build' 'python-installer')
makedepends=('python-build' 'python-installer' 'python-wheel')
optdepends=('python-matplotlib: to use all MDAnalysis functions'
            'python-joblib: to use all MDAnalysis functions'
            'python-netcdf4: to operate on AMBER binary trajectories'
            'python-networkx: for analysis of lipid leaflets via MDAnalysis.analysis.leaflet')
source=("https://github.com/MDAnalysis/mdanalysis/archive/refs/tags/package-${pkgver}.tar.gz"
        '0001-Allow-using-newer-numpy.patch')
sha256sums=('7bfc21d4777d84120421df742be56664e0b7b6a1b6b4faf307db39cf23764a47'
            '964e9e7550f2e4833290f4e89c3d6ef81aca352e0759ec89e7923d81e6c4ca83')

CFLAGS+=" -Wno-incompatible-pointer-types"

prepare() {
  cd $srcdir/mdanalysis-package-$pkgver
  patch -p1 -i $srcdir/0001-Allow-using-newer-numpy.patch
}

build() {
  cd $srcdir/mdanalysis-package-$pkgver/package
  export CFLAGS
  python -m build --wheel --no-isolation
}

package(){
  cd $srcdir/mdanalysis-package-$pkgver/package
  python -m installer --destdir="$pkgdir" dist/*.whl
}
