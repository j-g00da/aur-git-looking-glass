# Maintainer: envolution
# Contributor: mdaniels5757 <arch at mdaniels dot me>
# Contributor: jskier <jay @jskier.com>
# shellcheck shell=bash disable=SC2034,SC2154
pkgname=keeper-secrets-manager-core
pkgver=17.0.0
pkgrel=1
_pypifilestem=${pkgname//-/_}
pkgdesc="Python SDK for Keeper Secrets Manager"
arch=('any')
url="https://pypi.org/project/keeper-secrets-manager-core/"
license=('MIT')
depends=('python'
  'python-requests>=2.28.2'
  'python-cryptography>=39.0.1'
  'python-importlib-metadata>=6.0.0'
)
makedepends=(
  'python-installer'
  'python-build'
  'python-setuptools'
)
source=("https://files.pythonhosted.org/packages/source/k/keeper_secrets_manager_core/keeper_secrets_manager_core-${pkgver}.tar.gz")
sha256sums=('f821d114b44ecd992870f4661d1daa5c50901d4abcc6d52c37926c372396d31c')

build() {
  cd "$_pypifilestem-$pkgver"
  python -m build --wheel --no-isolation
}
package() {
  cd "$_pypifilestem-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -D -m644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
# vim:set ts=2 sw=2 et:
