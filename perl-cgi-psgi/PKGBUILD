# Maintainer: Felix Golatofski <contact@xdfr.de>
# Contributor: Max Pray a.k.a. Synthead <synthead@gmail.com>
# Contributor: Ruben Kelevra <ruben@vfn-nrw.de>

pkgname=perl-cgi-psgi
_cpanname="CGI-PSGI"
pkgver=0.15
pkgrel=4
pkgdesc="Adapt CGI.pm to the PSGI protocol"
arch=('any')
url="https://search.cpan.org/~miyagawa/$_cpanname-$pkgver/"
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/M/MI/MIYAGAWA/${_cpanname}-${pkgver}.tar.gz")
sha256sums=('c50dcb10bf8486a9843baed032ad89d879ff2f41c993342dead62f947a598d91')

# Function to change to the working directory and set
# environment variables to override undesired options.
prepareEnvironment() {
	cd "$srcdir/$_cpanname-$pkgver"
	export \
		PERL_MM_USE_DEFAULT=1 \
		PERL_AUTOINSTALL=--skipdeps \
		PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
		PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
		MODULEBUILDRC=/dev/null
}

build() {
	prepareEnvironment
	/usr/bin/perl Makefile.PL
	make
}

check() {
	prepareEnvironment
	make test
}

package() {
	prepareEnvironment
	make install

	# Remove "perllocal.pod" and ".packlist".
	find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
