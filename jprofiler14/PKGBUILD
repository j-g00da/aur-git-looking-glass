# Maintainer: Phillip Schichtel <phillip.public@schich.tel>
pkgname=jprofiler14
pkgver=14.0.6
pkgrel=1
options=(!strip)
pkgdesc="The Award-Winning All-in-One Java Profiler"
arch=(any)
url="http://jprofiler.com"
license=('custom')
depends=('java-environment')
source=("https://download-gcdn.ej-technologies.com/jprofiler/jprofiler_linux_$(echo $pkgver | sed 's/\./_/g').tar.gz"
        "${pkgname}.desktop"
        "${pkgname}.sh")
sha256sums=('74f46eaafca7ff1735cf05b25a19fb9605f813551c04503a0326555ec5907a37'
            'a79b87452d5b3f0b514b5651fa6add712ff66bbaceef23f6fdcded1d0ee028b9'
            'e60a82c65ad102ec7092c3cd8aaaa2367b568d408174cfd313d792933e3862cd')

package() {
    local optdir="/opt/${pkgname}"
    local target="${pkgdir}${optdir}"
    local extracted="${srcdir}/jprofiler${pkgver}"
    install -dm755 "$target"
    install -dm755 "${pkgdir}/usr/bin"
    install -m755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
    cp -dpr --no-preserve=ownership "${extracted}/." "$target"

    install -dm755 "${pkgdir}/etc"
    ln -s "${optdir}/config" "${pkgdir}/etc/${pkgname}"

    local hicolor="${pkgdir}/usr/share/icons/hicolor/"
    install -dm755 "${hicolor}/512x512/apps"
    install -dm755 "${hicolor}/256x256/apps"
    install -dm755 "${hicolor}/64x64/apps"
    install -dm755 "${hicolor}/32x32/apps"
    install -dm755 "${hicolor}/16x16/apps"

    local licensedir="${pkgdir}/usr/share/licenses/${pkgname}"
    install -dm755 "$licensedir"
    mv "${target}/license.txt" "$licensedir"
    mv "${target}/license.html" "$licensedir"

    local image_var="1u09tly"
    cp "${target}/.install4j/i4j_extf_6_${image_var}_92xp8q@2x.png" "${hicolor}/512x512/apps/${pkgname}.png"
    cp "${target}/.install4j/i4j_extf_6_${image_var}_92xp8q.png" "${hicolor}/256x256/apps/${pkgname}.png"
    cp "${target}/.install4j/i4j_extf_5_${image_var}_1x4rayh@2x.png" "${hicolor}/64x64/apps/${pkgname}.png"
    cp "${target}/.install4j/i4j_extf_5_${image_var}_1x4rayh.png" "${hicolor}/32x32/apps/${pkgname}.png"
    cp "${target}/.install4j/i4j_extf_4_${image_var}_ozp0zm.png" "${hicolor}/16x16/apps/${pkgname}.png"

    local appsdir="${pkgdir}/usr/share/applications"
    install -dm755 "$appsdir"
    cp "${srcdir}/${pkgname}.desktop" "$appsdir"
}
