# Maintainer: Tobias Bachmann <tobachmann@gmx.de>
pkgname=connectome-workbench
pkgver=2.0.1
pkgrel=1
pkgdesc="Connectome Workbench is an open source, freely available visualization and discovery tool used to map neuroimaging data, especially data generated by the Human Connectome Project."
arch=('x86_64')
url="http://humanconnectome.org/software/connectome-workbench"
license=('GPL')
depends=('qt5-base')
makedepends=('cmake')
source=("https://github.com/Washington-University/workbench/archive/v$pkgver.tar.gz"
        "$pkgname.desktop"
	"001-include_cstdint_in_libCZI_exceptions.h.patch")
sha256sums=('c80bb248d1d25b36dd92112b9d3fb4585474e117c0c49dc8a222d859624f0376'
            '9e002a2bc4e8354a0514e6e50c7ae388db9f243341385a7aaa8870954acbad96'
            '1998a2fba2f5cad74b889c7d2bcab31e9c3c75707b95a7d6ddd29f9c6ddddcb8')

prepare() {
  cd "workbench-$pkgver"
  patch -p1 < "${srcdir}"/001-include_cstdint_in_libCZI_exceptions.h.patch
}

build() {
  mkdir build
  cd build
  cmake -D CMAKE_CXX_FLAGS="-fpermissive" -D CMAKE_INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release -D WORKBENCH_MESA_DIR=/usr -D WORKBENCH_USE_QT5=TRUE ../workbench-$pkgver/src
  make -j $(nproc)
}

check() {
  cd build
  ctest
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
  mkdir -p $pkgdir/usr/share/$pkgname/icons
  install ../workbench-$pkgver/icons/linux/workbench_128x128x32.png $pkgdir/usr/share/$pkgname/icons/workbench_128x128x32.png
  mkdir -p $pkgdir/usr/share/applications
  install ../../$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
}

post_install() {
  update-desktop-database
}
