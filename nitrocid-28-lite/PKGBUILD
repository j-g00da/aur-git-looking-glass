# Maintainer: Aptivi <ceo at aptivi dot anonaddy dot com>
pkgname=nitrocid-28-lite
pkgver=4.0.28.18+0.2.0.0~beta
pkgrel=1
pkgdesc="Simulates our future-planned kernel"
arch=('x86_64' 'aarch64')
url="https://aptivi.github.io"
license=('GPL-3.0-or-later')
depends=('dotnet-runtime-8.0' 'tzdata')
makedepends=('git' 'dotnet-sdk-8.0' 'make' 'which')
optdepends=('jack2: Jack support for BassBoom addon'
			'portaudio: PortAudio support for BassBoom addon'
			'openal: OpenAL support for BassBoom addon'
			'sdl2: SDL support for BassBoom addon'
			'libpulse: PulseAudio support for BassBoom addon')
provides=("${pkgname}-git" "${pkgname%-lite}" "${pkgname%-lite}-git")
conflicts=("${pkgname}-git" "${pkgname%-lite}" "${pkgname%-lite}-git")
options=('!strip')
source=("${pkgname}::git+https://gitlab.com/aptivi/main/Nitrocid#tag=v0.2.0.0-beta")
sha256sums=('SKIP')

prepare() {
	cd "${pkgname}"
	make init-offline
	make clean
	git submodule update --init --remote
}

build() {
	cd "${pkgname}"
	make all-offline BUILDARGS="-p:NKSLITE=true"
}

package() {
	cd "${pkgname}"
	make install DESTDIR="$pkgdir"
}
