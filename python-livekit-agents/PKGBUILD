# Maintainer: Mohamed Amine Zghal (medaminezghal) <medaminezghal at outlook dot com>

_name0=livekit-agents
_name1=livekit-plugins
_plugins=(anam anthropic assemblyai aws azure baseten bey bithuman cartesia clova deepgram elevenlabs fal fireworksai gladia google groq hedra hume inworld langchain lmnt minimal minimax mistralai neuphonic nltk openai resemble rime rtzr sarvam silero simli smallestai soniox speechify speechmatics spitch tavus turn-detector ultravox upliftai)
pkgbase=python-$_name0
pkgname=(python-$_name0 ${_plugins[@]/#/python-$_name1-})
pkgver=1.2.16
pkgrel=1
_plugins_pkgdesc=('Agent Framework plugin for anam.'
                  'Agent Framework plugin for services from Anthropic.'
                  'Agent Framework plugin for AssemblyAI.'
                  'LiveKit Agents Plugin for services from AWS.'
                  'Agent Framework plugin for services from Azure.'
                  'Agent Framework plugin for Baseten.'
                  'Agent Framework plugin for services from Beyond Presence.'
                  'Agent Framework plugin for services from BitHuman Avatar Rendering.'
                  'LiveKit Agents Plugin for Cartesia.'
                  'LiveKit Agents Plugin for LINE Clova STT.'
                  "Agent Framework plugin for services using Deepgram's API."
                  "Agent Framework plugin for voice synthesis with ElevenLabs' API."
                  'fal plugin template for LiveKit Agents.'
                  'LiveKit Agents Plugin for Fireworks AI.'
                  "Agent Framework plugin for services using Gladia's API."
                  'Agent Framework plugin for services from Google Cloud.'
                  'Groq inference plugin for LiveKit Agents.'
                  'Agent Framework plugin for Hedra Avatar.'
                  'Hume TTS plugin for LiveKit agents.'
                  "Agent Framework plugin for voice synthesis with Inworld's API."
                  'LangChain/LangGraph plugin for LiveKit agents.'
                  'LMNT TTS plugin for LiveKit agents.'
                  'Minimal plugin template for LiveKit Agents.'
                  'LiveKit Agents Plugin for services from AWS'
                  'LiveKit Agents Plugin for services from AWS.'
                  'Neuphonic inference plugin for LiveKit Agents.'
                  'Agent Framework plugin for NLTK-based text processing.'
                  'Agent Framework plugin for services from OpenAI.'
                  'LiveKit Agents Plugin for Resemble AI.'
                  'LiveKit Agents Plugin for Rime.'
                  'Agent Framework plugin for RTZR Streaming STT.'
                  "Agent Framework plugin for services using Sarvam.ai's API."
                  'Agent Framework Plugin for Silero.'
                  'Agent Framework plugin for Simli.'
                  'Agent Framework plugin for speech synthesis with the Smallest AI.'
                  "Agent Framework plugin for services using Soniox's API."
                  "Agent Framework plugin for voice synthesis with Speechify's API."
                  'Agent Framework plugin for Speechmatics.'
                  'spitch plugin template for LiveKit Agents.'
                  'Agent Framework plugin for Tavus.'
                  'End of utterance detection for LiveKit Agents.'
                  'Agent Framework plugin for services from Ultravox.'
                  'Agent Framework plugin for speech synthesis with the Uplift AI.')
_plugins_depends=("'python-livekit-agents'"
                  "'python-livekit-agents' 'python-anthropic' 'python-httpx'"
                  "'python-livekit-agents'"
                  "'python-livekit-agents' 'python-aioboto3' 'python-amazon-transcribe'"
                  "'python-livekit-agents' 'python-azure-cognitiveservices-speech'"
                  "'python-livekit-agents' 'python-aiohttp' 'python-livekit'"
                  "'python-livekit-agents'"
                  "'python-livekit-agents' 'python-bithuman'"
                  "'python-livekit-agents'"
                  "'python-livekit-agents' 'python-pydub'"
                  "'python-livekit-agents' 'python-av' 'python-numpy'"
                  "'python-livekit-agents' 'python-av' 'python-numpy'"
                  "'python-livekit-agents' 'python-fal-client'"
                  "'python-livekit-agents'"
                  "'python-livekit-agents' 'python-av' 'python-numpy' 'python-aiohttp'"
                  "'python-google-auth' 'python-google-cloud-speech' 'python-google-cloud-texttospeech' 'python-google-genai' 'python-livekit-agents'"
                  "'python-livekit-agents' 'python-av' 'python-numpy' 'python-livekit-plugins-openai' 'python-aiohttp' 'python-livekit'"
                  "'python-livekit-agents'"
                  "'python-aiohttp' 'python-livekit-agents'"
                  "'python-livekit-agents'"
                  "'python-livekit-agents' 'python-langchain-core' 'python-langgraph'"
                  "'python-livekit-agents'"
                  "'python-livekit-agents'"
                  "'python-livekit-agents' 'python-av' 'python-numpy'"
                  "'python-livekit-agents' 'python-livekit-plugins-openai' 'python-mistralai'"
                  "'python-livekit-agents'"
                  "'python-livekit-agents' 'python-nltk'"
                  "'python-livekit-agents' 'python-av' 'python-numpy' 'python-pillow' 'python-openai'"
                  "'python-livekit-agents'"
                  "'python-livekit-agents' 'python-av' 'python-numpy'"
                  "'python-livekit-agents' 'python-httpx' 'python-aiohttp'"
                  "'python-livekit-agents' 'python-av' 'python-numpy'"
                  "'python-livekit-agents' 'python-onnxruntime' 'python-numpy'"
                  "'python-livekit-agents'"
                  "'python-livekit-agents' 'python-av' 'python-numpy'"
                  "'python-livekit-agents'"
                  "'python-livekit-agents' 'python-av' 'python-numpy'"
                  "'python-livekit-agents' 'python-speechmatics-rt'"
                  "'python-livekit-agents' 'python-av' 'python-numpy' 'python-spitch'"
                  "'python-livekit-agents'"
                  "'python-livekit-agents' 'python-transformers' 'python-numpy' 'python-onnxruntime' 'python-jinja'"
                  "'python-livekit-agents' 'python-av' 'python-numpy'"
                  "'python-livekit-agents' 'python-av' 'python-numpy'")
_plugins__optdepends=(""
                      ""
                      ""
                      "'python-aws-sdk-bedrock-runtime: realtime' 'python-aws-sdk-signers: realtime' 'python-boto3: realtime'"
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      "'python-google-auth: vertex'"
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      ""
                      "")
arch=('x86_64' 'aarch64')
_repo='https://github.com/livekit/agents'
license=('Apache-2.0')
depends=('python')
makedepends=('python-hatchling' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-dotenv' 'python-pytest' 'python-pytest-asyncio' 'python-jiwer' 'python-scipy' 'python-tiktoken' 'python-nltk' 'nltk-data' 'python-docstring-parser' 'python-speechmatics-rt')
source=("$_repo/archive/refs/tags/$_name0@$pkgver.tar.gz"
        "$_repo/raw/refs/tags/$_name0@$pkgver/$_name1/$_name1-silero/${_name1//-//}/silero/resources/silero_vad.onnx")
sha256sums=('92d89bb0da07c3e54015035fb61a83c2f0faebf54729a263a8d024f1138714f8'
            '597d30b3ec076608d059477bb14cfeffdf951bf5cae370d38f65d33bbfe82004')

prepare(){
  cp -f "$srcdir"/silero_vad.onnx "$srcdir"/${_name0//livekit-/}-$_name0-$pkgver/$_name1/$_name1-silero/${_name1//-//}/silero/resources/silero_vad.onnx
  cd "$srcdir"/${_name0//livekit-/}-$_name0-$pkgver
  sed -i 's/# //g' tests/utils.py
  sed -i 's/resample if not None/# resample if not None/g' tests/utils.py
  sed -i '2i\import pathlib' tests/utils.py
  sed -i '2i\import os' tests/utils.py
}

build() {
  cd "$srcdir"/${_name0//livekit-/}-$_name0-$pkgver/$_name0
  python -m build --wheel --no-isolation
  for _plugin in "${_plugins[@]}"; do
    cd "$srcdir"/${_name0//livekit-/}-$_name0-$pkgver/$_name1/$_name1-$_plugin
    python -m build --wheel --no-isolation
  done
}

check() {
  local pytest_options=(
    -vv
    # Import problem need to be fixed by developers
    --ignore tests/test_llm.py
    --ignore tests/test_vad.py
    --ignore tests/test_ipc.py
    --ignore tests/test_connection_pool.py
    -k "not test_two_speakers_simple_alternation and not test_none_speaker_id"
    # Need API's
    --deselect tests/test_audio_decoder.py::test_decode_and_transcribe
    --deselect tests/test_stt.py::test_recognize
    --deselect tests/test_stt.py::test_stream
    --deselect tests/test_tts.py::test_tts_synthesize
    --deselect tests/test_tts.py::test_tts_synthesize_timeout
    --deselect tests/test_tts.py::test_tts_stream
    --deselect tests/test_tts.py::test_tts_stream_timeout
    --deselect tests/test_evals.py::test_function_call
    --deselect tests/test_evals.py::test_inline_agent
    --deselect tests/test_evals.py::test_start_with_capture_run
    --deselect tests/test_workflows.py::test_collect_email
  )
  cd "$srcdir"/${_name0//livekit-/}-$_name0-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer $_name0/dist/*.whl
  for _plugin in "${_plugins[@]}"; do
    test-env/bin/python -m installer $_name1/$_name1-$_plugin/dist/*.whl
  done
  test-env/bin/python -m pytest "${pytest_options[@]}" tests
}

package_python-livekit-agents() {
  pkgdesc='A powerful framework for building realtime voice AI agents.'
  url='https://github.com/livekit/agents/tree/main/livekit-agents'
  depends+=('python-click' 'python-certifi' 'python-livekit' 'python-livekit-api' 'python-livekit-protocol' 'python-livekit-blingfire' 'python-protobuf' 'python-pyjwt' 'python-watchfiles' 'python-psutil' 'python-aiohttp' 'python-typing_extensions' 'python-sounddevice' 'python-docstring-parser' 'python-colorama' 'python-av' 'python-numpy' 'python-pydantic' 'python-nest-asyncio' 'python-opentelemetry-api' 'python-opentelemetry-sdk' 'python-opentelemetry-exporter-otlp' 'python-prometheus_client' 'python-openai')
  optdepends=('python-mcp: mcp'
              'python-av: codecs' 'python-numpy: codecs'
              'python-pillow: images'
              'python-livekit-plugins-anam: anam'
              'python-livekit-plugins-anthropic: anthropic'
              'python-livekit-plugins-assemblyai: assemblyai'
              'python-livekit-plugins-aws: aws'
              'python-livekit-plugins-azure: azure'
              'python-livekit-plugins-baseten: baseten'
              'python-livekit-plugins-bey: bey'
              'python-livekit-plugins-bithuman: bithuman'
              'python-livekit-plugins-cartesia: cartesia'
              'python-livekit-plugins-clova: clova'
              'python-livekit-plugins-deepgram: deepgram'
              'python-livekit-plugins-elevenlabs: elevenlabs'
              'python-livekit-plugins-fal: fal'
              'python-livekit-plugins-fireworksai: fireworksai'
              'python-livekit-plugins-gladia: gladia'
              'python-livekit-plugins-google: google'
              'python-livekit-plugins-groq: groq'
              'python-livekit-plugins-hedra: hedra'
              'python-livekit-plugins-hume: hume'
              'python-livekit-plugins-inworld: inworld'
              'python-livekit-plugins-langchain: langchain'
              'python-livekit-plugins-lmnt: lmnt'
              'python-livekit-plugins-minimax-ai: minimax'
              'python-livekit-plugins-mistralai: mistralai'
              'python-livekit-plugins-neuphonic: neuphonic'
              'python-livekit-plugins-nltk: nltk'
              'python-livekit-plugins-openai: openai'
              'python-livekit-plugins-resemble: resemble'
              'python-livekit-plugins-rime: rime'
              'python-livekit-plugins-sarvam: sarvam'
              'python-livekit-plugins-silero: silero'
              'python-livekit-plugins-simli: simli'
              'python-livekit-plugins-smallestai: smallestai'
              'python-livekit-plugins-soniox: soniox'
              'python-livekit-plugins-speechify: speechify'
              'python-livekit-plugins-speechmatics: speechmatics'
              'python-livekit-plugins-spitch: spitch'
              'python-livekit-plugins-tavus: tavus'
              'python-livekit-plugins-turn-detector: turn-detector'
              'python-livekit-plugins-ultravox: ultravox'
              'python-livekit-plugins-upliftai: upliftai')
  cd "$srcdir"/${_name0//livekit-/}-$_name0-$pkgver
  python -m installer --destdir="$pkgdir" $_name0/dist/*.whl
}

livekit-plugins(){
  for ((i=0; i<${#_plugins[@]}; i++)); do
    eval "package_python-$_name1-${_plugins[i]}() {
            pkgdesc=\"${_plugins_pkgdesc[i]}\"
            url=\"$_repo\"/tree/main/$_name1/$_name1-${_plugins[i]}
            depends=(${_plugins_depends[i]})
            optdepends=(${_plugins__optdepends[i]})
            cd \"\$srcdir\"/${_name0//livekit-/}-$_name0-$pkgver/$_name1/$_name1-${_plugins[i]}
            python -m installer --destdir=\"\$pkgdir\" dist/*.whl
          }"
  done
}

livekit-plugins
