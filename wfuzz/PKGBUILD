# Maintainer: Network Jack <Network_Jack@null.net>
# Contributor: Denis Kasak <dkasak AT termina DOT org DOT uk>
# Contributor: Cedric Girard <cgirard.archlinux@valinor.fr>
# Contributor: Olivier Le Moal <mail at olivierlemoal dot fr>
# Contributor: Dawid Wrobel <cromo@klej.net>
# Contributor: SÃ©bastien Duquette <ekse.0x@gmail.com>
# Contributor: onny <onny@project-insanity.org>

pkgname=wfuzz
pkgver=3.1.0
pkgrel=2
pkgdesc="Utility to bruteforce web applications to find their not linked resources"
url="https://github.com/xmendez/wfuzz"
arch=('any')
license=('GPL')
depends=('python-pycurl' 'python-pyparsing' 'python-chardet' 'python-six' 'python-legacy-cgi')
makedepends=('python-setuptools' 'python-sphinx')
source=("https://github.com/xmendez/wfuzz/archive/v$pkgver.tar.gz"
        setup.py.diff)
sha256sums=('60798e56274a377f8770d162935a040d4d6e27919bfbb46cf13a7a0a34896be9'
            '275e9811517da830ac923ca8bbb55732930b6a264d27cfbc6df5b26d5c9ba377')

prepare() {
  cp "$srcdir/setup.py.diff" "$srcdir/$pkgname-$pkgver"
  cd "$srcdir/$pkgname-$pkgver"
  patch -p0 < "setup.py.diff"
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}/docs"
    python conf.py
    make SPHINXOPTS='-Q -j auto' man html
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    install -Dt "$pkgdir/usr/share/man/man1" docs/_build/man/*.1
    install -Dt "$pkgdir/usr/share/doc/${pkgname}" -m644 README.md
    cp -prt "$pkgdir/usr/share/doc/${pkgname}" docs/_build/html

    install -dm755 "$pkgdir/usr/share/${pkgname}/wordlists"
    cp -prt "$pkgdir/usr/share/${pkgname}/wordlists" wordlist/*

    install -Dm644 *_bash_completion "$pkgdir/etc/bash_completion.d/${pkgname}"

    python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
}
