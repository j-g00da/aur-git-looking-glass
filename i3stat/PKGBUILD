# Maintainer: Callum Osmotherly <acheronfail@gmail.com>
pkgname=i3stat
pkgver=0.15.0
pkgrel=1
pkgdesc="A lightweight and batteries-included status_command for i3 and sway"
arch=('x86_64')
url="https://github.com/acheronfail/i3stat"
license=('GPL-3.0-only')
conflicts=('i3stat-bin' 'i3stat-git')
depends=('gcc-libs')
makedepends=('cargo' 'clang' 'dbus' 'dunst' 'libfaketime' 'libpulse' 'i3-wm' 'imagemagick' 'scrot' 'xorg-server-xvfb')
optdepends=(
  'dbus: sending notifications'
  'dunst: showing if notifications are paused or not via dunst'
  'mako: showing if notifications are paused or not via mako'
  'i3-wm: use it as the status_command'
  'sway: use it as the status_command'
)
source=("$pkgname-$pkgver.tar.gz::https://static.crates.io/crates/$pkgname/$pkgname-${pkgver//_/-}.crate")
sha512sums=('d28b30fee75e16df3f24237f8590e995579a5b860228d1b475c77c1df4c6e40669d444f2817c928af722e72ab70b1b8ba95ed88cd36ebc010b7ccd4d4f83e145')

# this is effectively a no-op for standard usages of the PKGBUILD, but it exists for
# local testing of the PKGBUILD itself in the upstream repository (when the build version
# doesn't match the PKGBUILD because it hasn't been released yet)
prepare() {
  _dirname=$(bsdtar -tf "${source[0]%%::*}" | awk -F / '{print $1; exit}')
  mv "$_dirname" "_$pkgname-$pkgver"
}

build() {
  cd "_$pkgname-$pkgver"

  # builds all the crate's binaries
  cargo build --release --locked

  # the manpages are generated by the tests
  cargo test --release --locked manpage
}

check() {
  cd "_$pkgname-$pkgver"

  dbus-run-session -- env I3STAT_TEST=1 cargo test --release --locked
}

package() {
  cd "_$pkgname-$pkgver"

  install -Dm755 "target/release/i3stat"         "$pkgdir/usr/bin/i3stat"
  install -Dm755 "target/release/i3stat-ipc"     "$pkgdir/usr/bin/i3stat-ipc"
  install -Dm755 "target/release/i3stat-acpi"    "$pkgdir/usr/bin/i3stat-acpi"
  install -Dm755 "target/release/i3stat-signals" "$pkgdir/usr/bin/i3stat-signals"
  install -Dm755 "target/release/i3stat-sensors" "$pkgdir/usr/bin/i3stat-sensors"

  install -Dm644 "doc/i3stat.1"                 "$pkgdir/usr/share/man/man1/i3stat.1"
  install -Dm644 "doc/i3stat-acpi.1"            "$pkgdir/usr/share/man/man1/i3stat-acpi.1"
  install -Dm644 "doc/i3stat-ipc.1"             "$pkgdir/usr/share/man/man1/i3stat-ipc.1"
  install -Dm644 "doc/i3stat-ipc-click.1"       "$pkgdir/usr/share/man/man1/i3stat-ipc-click.1"
  install -Dm644 "doc/i3stat-ipc-custom.1"      "$pkgdir/usr/share/man/man1/i3stat-ipc-custom.1"
  install -Dm644 "doc/i3stat-ipc-get-bar.1"     "$pkgdir/usr/share/man/man1/i3stat-ipc-get-bar.1"
  install -Dm644 "doc/i3stat-ipc-get-config.1"  "$pkgdir/usr/share/man/man1/i3stat-ipc-get-config.1"
  install -Dm644 "doc/i3stat-ipc-get-theme.1"   "$pkgdir/usr/share/man/man1/i3stat-ipc-get-theme.1"
  install -Dm644 "doc/i3stat-ipc-info.1"        "$pkgdir/usr/share/man/man1/i3stat-ipc-info.1"
  install -Dm644 "doc/i3stat-ipc-refresh-all.1" "$pkgdir/usr/share/man/man1/i3stat-ipc-refresh-all.1"
  install -Dm644 "doc/i3stat-ipc-set-theme.1"   "$pkgdir/usr/share/man/man1/i3stat-ipc-set-theme.1"
  install -Dm644 "doc/i3stat-ipc-shutdown.1"    "$pkgdir/usr/share/man/man1/i3stat-ipc-shutdown.1"
  install -Dm644 "doc/i3stat-ipc-signal.1"      "$pkgdir/usr/share/man/man1/i3stat-ipc-signal.1"
  install -Dm644 "doc/i3stat-sensors.1"         "$pkgdir/usr/share/man/man1/i3stat-sensors.1"
  install -Dm644 "doc/i3stat-signals.1"         "$pkgdir/usr/share/man/man1/i3stat-signals.1"

  install -Dm644 "README.md"                   "$pkgdir/usr/share/doc/${pkgname}/README.md"
  install -Dm644 "sample_config.toml"          "$pkgdir/usr/share/doc/${pkgname}/sample_config.toml"
  install -Dm644 "sample_included_config.toml" "$pkgdir/usr/share/doc/${pkgname}/sample_included_config.toml"
  install -Dm644 "LICENSE"                     "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
