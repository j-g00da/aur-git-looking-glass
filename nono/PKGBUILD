# Maintainer: Adrian Perez de Castro <aperez@igalia.com>
pkgdesc='Omron LUNA-I/LUNA-88K, Sharp X68030, and NEWS NWS-1750 emulator'
pkgname=nono
pkgver=1.6.3
pkgrel=1
url=http://www.pastel-flower.jp/~isaki/nono
license=(custom)
arch=(x86_64)
depends=(alsa-lib libbsd libkqueue libslirp termcap wxwidgets-gtk3)
makedepends=(bmake freebsd-mk gettext)
source=("${url}/archive/${pkgname}-${pkgver}.tar.gz")
b2sums=('e88a2d0c11b97d1fd281e96b18c5d3a657898614378facab7524ce3c2745e294ab73860fe6720a06d05ea633de193c5e05247b4564387ae44135c5ed53c80fb7')

prepare ()
{
	# The configure script searches for <kqueue/sys/event.h> instead of the
	# regular location <sys/event.h> as used by the libkqueue package, so
	# provide a local directory with the expected layout.
	mkdir -p "${srcdir}/include/kqueue/"
	ln -vsnf /usr/include/sys "${srcdir}/include/kqueue/sys"
}

build ()
{
	cd "${pkgname}-${pkgver}"

	./configure \
		CPPFLAGS="${CPPFLAGS} -I${srcdir}/include" \
		WX_CONFIG=/usr/bin/wx-config-gtk3 \
		--prefix=/usr

	# The configuration generated by autoconf does NOT include CPPFLAGS,
	# so add the include directory for the mangled libkqueue headers now.
	sed -i "/^C\(C\|XX\)=/s,$, -I${srcdir}/include," \
		Makefile.cfg

	echo 'CXXFLAGS+= -Wno-error=template-id-cdtor -Wno-error=deprecated-declarations' >> Makefile.cfg

	bmake -DNOTEST -m/usr/share/mk-freebsd
}

package ()
{
	cd "${pkgname}-${pkgver}"
	bmake -DNOTEST -m/usr/share/mk-freebsd DESTDIR="${pkgdir}" install
	install -Dm644 doc/nono-license.txt \
		"${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}
