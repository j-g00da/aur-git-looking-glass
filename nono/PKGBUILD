# Maintainer: Adrian Perez de Castro <aperez@igalia.com>
pkgdesc='Omron LUNA-I/LUNA-88K, Sharp X68030, and NEWS NWS-1750 emulator'
pkgname=nono
pkgver=1.6.1
pkgrel=1
url=http://www.pastel-flower.jp/~isaki/nono
license=(custom)
arch=(x86_64)
depends=(alsa-lib libbsd libkqueue libslirp termcap wxwidgets-gtk3)
makedepends=(bmake freebsd-mk gettext)
source=("${url}/archive/${pkgname}-${pkgver}.tar.gz")
b2sums=('2b0f2ef57b9bb6ad2c2beac78cfeb48c0cea84059d599ea7ac1630089826f2b11db89158dd38620b72c7815d04d75bddf92f1db4ca4b17b5e623f60a27ac10f4')

prepare ()
{
	# The configure script searches for <kqueue/sys/event.h> instead of the
	# regular location <sys/event.h> as used by the libkqueue package, so
	# provide a local directory with the expected layout.
	mkdir -p "${srcdir}/include/kqueue/"
	ln -vsnf /usr/include/sys "${srcdir}/include/kqueue/sys"
}

build ()
{
	cd "${pkgname}-${pkgver}"

	./configure \
		CPPFLAGS="${CPPFLAGS} -I${srcdir}/include" \
		WX_CONFIG=/usr/bin/wx-config-gtk3 \
		--prefix=/usr

	# The configuration generated by autoconf does NOT include CPPFLAGS,
	# so add the include directory for the mangled libkqueue headers now.
	sed -i "/^C\(C\|XX\)=/s,$, -I${srcdir}/include," \
		Makefile.cfg

	echo 'CXXFLAGS+= -Wno-error=template-id-cdtor -Wno-error=deprecated-declarations' >> Makefile.cfg

	bmake -DNOTEST -m/usr/share/mk-freebsd
}

package ()
{
	cd "${pkgname}-${pkgver}"
	bmake -DNOTEST -m/usr/share/mk-freebsd DESTDIR="${pkgdir}" install
	install -Dm644 doc/nono-license.txt \
		"${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}
