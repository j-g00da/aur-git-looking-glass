#
# Maintainer: Carlos Aznar√°n <caznaranl@uni.pe>
# Contributor: wangjiezhe <wangjiezhe@gmail.com>
# Contributor: Clemens Buchacher <drizzd@aon.at>
#
# You can use the newpkg script from
# https://github.com/drizzd/octave-forge-archlinux to automatically generate
# new octave-forge PKGBUILDs or update existing ones. Patches welcome.
#

_pack=octproj
pkgname=octave-${_pack}
pkgver=3.1.0
pkgrel=1
pkgdesc="This package allows to call functions of PROJ library for cartographic projections and CRS transformations"
arch=(any)
url="https://bitbucket.org/jgpallero/${_pack}"
license=(GPL-3.0-or-later)
groups=('octave-forge')
depends=('octave>=3.0.0' 'proj')
makedepends=()
optdepends=()
backup=()
options=()
install=${pkgname}.install
_archive=${_pack}-${pkgver}.tar.gz
source=("${_archive}::${url}/downloads/${_archive}")
noextract=("${_archive}")
sha512sums=('edab4e0bd5562f1170d1ca00a0807cea4ab9f4c73a106fe02108feec783a5104feafb9326a9967e228446187f393537a5d652fc0884303291ed278e77b4f91a2')

_octave_run() {
  octave --no-history --no-init-file --no-window-system -q -f --eval "$*"
}

_install_dir() {
  src=$1
  dst=$2
  mkdir -p "$(dirname "$dst")"
  cp -rT "$src" "$dst"
}

build() {
  _prefix="$srcdir"/install_prefix
  _archprefix="$srcdir"/install_archprefix
  mkdir -p "$_prefix" "$_archprefix"
  cd "$srcdir"
  _octave_run "$(
    cat <<-EOF
		pkg local_list octave_packages;
		pkg prefix $_prefix $_archprefix;
		pkg install -verbose -nodeps $_archive;
		EOF
  )"
}

package() {
  prefix=$pkgdir/usr/share/octave/packages
  archprefix=$pkgdir/usr/lib/octave/packages
  _install_dir "$srcdir"/install_prefix "$prefix"
  _install_dir "$srcdir"/install_archprefix "$archprefix"
}
