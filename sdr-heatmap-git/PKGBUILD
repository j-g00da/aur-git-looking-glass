# Maintainer: Jason Nader <jason.nader@protonmail.com>

pkgname=sdr-heatmap-git
pkgver=0.1.5.r229.g07041d4
pkgrel=1
pkgdesc='A tool to visualize files generated by rtl_power, inspired by heatmap.py'
arch=('x86_64')
url='https://github.com/j2ghz/sdr-heatmap'
license=('AGPL3')
depends=('gcc-libs')
makedepends=('git' 'cargo')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
source=("${pkgname%-*}::git+$url")
sha512sums=('SKIP')

pkgver() {
    cd "${pkgname%-*}"
    echo $(grep '^version =' Cargo.toml|head -n1|cut -d\' -f2).r$(git rev-list --count HEAD).g$(git describe --always)
}

prepare() {
    cd "${pkgname%-*}"
    cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
    cd "${pkgname%-*}"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release --all-features
}

package() {
    cd "${pkgname%-*}"
    install -Dm755 "target/release/${pkgname%-*}" "$pkgdir/usr/bin/${pkgname%-*}"
}
