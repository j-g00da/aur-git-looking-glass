# Maintainer: Danya Generalov <danya@danya02.ru>

# Generated by debtap and lightly edited
# README!!: when editing, don't forget to "makepkg --printsrcinfo > .SRCINFO"
# Also, consider using namcap to validate package


pkgname=proxmox-backup-client-bin
pkgdesc="Client for Proxmox Backup Server (binary release from Debian)"
arch=('x86_64')
url="https://pbs.proxmox.com"
license=('AGPL-3.0-or-later')
depends=('acl>=2.2.23' 'fuse3>=3.2.3' 'openssl>=3.0.0' 'zstd>=1.5.2' 'gcc-libs>=15.1.1' 'glibc>=2.27')
makedepends=('patchelf>=0.18.0')
options=('!strip' '!emptydirs')

pkgver=3.4.6_1
pkgrel=1
source_x86_64=("http://download.proxmox.com/debian/pbs-client/dists/bookworm/main/binary-amd64/proxmox-backup-client_3.4.6-1_amd64.deb")
sha512sums_x86_64=('5be9da65584915b79996713d81005ecc3b6477dea29e7338d308def9285ac2bef615e4e2a6a800523a838633bf32dfb08a8bc61818e932019c5360f7ef067b71')

conflicts=('proxmox-backup-client')

package(){

	# Extract package data
	tar -xJ -f data.tar.xz -C "${pkgdir}"

	install -D -m644 "${pkgdir}/usr/share/doc/proxmox-backup-client/copyright" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-AGPL3"

        # Replace dependency as suggested by https://aur.archlinux.org/packages/proxmox-backup-client-bin#comment-1026835.
        # Required because Arch's version of libfuse is newer than Debian's.
        # Before releasing any new version, attempt to run without these lines

        patchelf --replace-needed libfuse3.so.3 libfuse3.so ${pkgdir}/usr/bin/proxmox-backup-client
        patchelf --replace-needed libfuse3.so.3 libfuse3.so ${pkgdir}/usr/bin/pxar
}
