# Contributor: bunburya (dev at bunburya dot eu))
pkgname='python-piecash'
_name=${pkgname#python-}
pkgver=1.2.1
pkgrel=1
pkgdesc="A Python library providing a simple and pythonic interface to GnuCash files stored in SQL."
arch=('any')
url="https://pypi.org/project/piecash/"
license=('MIT')
depends=(
    'python'
    'python-babel'
    'python-sqlalchemy1.4'
    'python-sqlalchemy-utils'
    'python-pytz'
    'python-tzlocal'
    'python-click'
)
makedepends=('python-setuptools')
checkdepends=('python-pandas' 'python-pytest')
optdepends=(
    'gnucash: to create, view and edit GnuCash files'
    'python-pandas: view data in Pandas DataFrame'
    'python-psycopg2: postgresql support'
    'python-pymysql: MySQL support'
    'python-requests: retrieve quotes/prices from Yahoo Finance'
)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('7f9c5c7342df3b9cd1457adf4b82fea430f14fa93be17e52c7dd41c02abf7104')

check() {
    cd "$_name-$pkgver"
    #these checks require specific locales
    PYTHONPATH="$PWD" pytest \
    --deselect tests/test_session.py::test_get_system_currency_mnemonic[fr_FR.UTF-8] \
    --deselect tests/test_ledger.py::test_out_write[options1]
}

build() {
    cd "$_name-$pkgver"
    python setup.py build
}

package() {
    cd "$_name-$pkgver"
    python setup.py install --root="$pkgdir" --optimize=1
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
