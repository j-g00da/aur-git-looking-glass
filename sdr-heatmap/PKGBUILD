# Maintainer: Jozef Holly <j2.00ghz@gmail.com>
pkgname=sdr-heatmap
pkgver=0.1.2
pkgrel=1
pkgdesc="A tool to visualize files generated by rtl_power, inspired by heatmap.py"
arch=('x86_64' 'armv7l')
url="https://github.com/j2ghz/sdr-heatmap"
license=('AGPL3')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::https://github.com/j2ghz/sdr-heatmap/archive/v$pkgver.tar.gz")
sha512sums=('ea5ab2b25290864cf9a3fef9d9428bee49c6912c52e97572834816e8f31249e3ccfcf615267ac65a444ade9295f0ae87bc062ad187fba6227a3608612dc4b3ad')

build() {
  cd "$pkgname-$pkgver"

  cargo build --release --locked --all-features
}

check() {
  cd "$pkgname-$pkgver"

  cargo test --release --locked
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm755 "target/release/sdr-heatmap" "$pkgdir/usr/bin/sdr-heatmap"
}
