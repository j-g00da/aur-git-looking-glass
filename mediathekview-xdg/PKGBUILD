# Maintainer: Max Harmathy <harmathy@mailbox.org>
# Contributor: David Runge <dvzrv@archlinux.org>

_name=MediathekView
_pkgname=mediathekview
pkgname=${_pkgname}-xdg
pkgver=14.3.1
pkgrel=2
pkgdesc="Access the Mediathek of many German TV stations (store data in XDG_DATA_HOME)"
arch=(any)
url="https://github.com/mediathekview/mediathekview"
license=(GPL-3.0-or-later)
_java_version=${JAVA_VERSION:-25}
depends=(
  bash
  hicolor-icon-theme
  java-runtime-openjdk="$_java_version"
  xdg-user-dirs
)
makedepends=(
  maven
  strip-nondeterminism
  java-environment-openjdk="$_java_version"
)
conflicts=(mediathek)
provides=(mediathek)
replaces=(mediathek)
optdepends=(
  'libnotify: to use desktop notifications'
  'mplayer: for recording streams'
  'vlc: for stream playback'
)
source=(
  "$_pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz"
  "de.$_pkgname.$_name.desktop"
  "$_pkgname.sh"
  Fix-class-path-for-dependencies.patch
)
sha512sums=('fbce28a2bf0156a8f30655b48e61d9ea939b96f0fb6222c4eb68e3b8feb943c5175780c4399e9985ab4d12c114dd81a320bee47d047c395d618a69ea179d6148'
            '0591f9ca8a79539c3b4bdf81980d83500267f03d6c657e6694f705bf504a2b9b3d846a0ba3c3e750d422812c38d8504c424a86362cbce1229cd603b98afcbee2'
            '249667213eadd9be3eaf888d5c18caed415971f3afa2d2e777a0567661d4e6d49f391a8e6e093a5b766d67b6b918a6620c8675a67a194ae0c8e7179750a55b1f'
            'b6ef32b5c0b9538e00fa7f32cc536c7a532df06e01ae43a1d88eebe091f9ea982079ec8402ba2436cc2e1e7b3756e4c90eb889d0c97b2b838724bbd5c715bdee')
b2sums=('4a5099e66f2f72737935a9892f7caa82f030d5be3a6d75bb6caf3a4a1e124913638c4d31713913c954ab95dad82ef7b5630bc9df2bc0c62a14bbdc9eeb72d912'
        'c00c4621c1de5ea82760d337e26f0b5287577144a1ff50f010b6148f88161d39778b42d70755d96f43dff13ce1329d069f04f9a74be90e7dd5f520be043d576f'
        '7e14eff619d6d67b000abc8420e8bd3c6fe77841365f539322890f5e7c06d6d61d089c199bc9b2f0a8598c9dae67fdb71e88f7a1f40a91316040cd60c7ba3e8e'
        '24a7153fe53cb7a82652e6f3abf486582500d6d40a152e8561c65d521aadf9b266c9e43b6ea725045284355b0f51ad8650ca9930cc0a805a29ec893fea57aa31')

prepare() {
  # check for correct java version
  local status=''
  read -ra javac_version < <(javac --version) || status='failed'
  if [ -n "$status" ] || [ ! "${javac_version[1]%%.*}" = "$_java_version" ]; then
    echo "Please set your java version to openjdk $_java_version using:"  >&2
    printf -- '\e[93;1m %s\e[0m' "archlinux-java set java-$_java_version-openjdk" >&2
    return 1
  fi

  # set java version for wrapper script
  sed "s/JAVA_VERSION/$_java_version/" "$_pkgname.sh" > "$_pkgname"

  cd $_name-$pkgver
  patch -Np1 < "${srcdir}"/Fix-class-path-for-dependencies.patch
}

build() {
  cd $_name-$pkgver
  ./mvnw clean install
  # Timestamps in JAR files generated by Maven do not honour SOURCE_DATE_EPOCH
  # (https://cwiki.apache.org/confluence/pages/viewpage.action?pageId=74682318)
  (
    PATH=$PATH:/usr/bin/vendor_perl
    shopt -s globstar
    for file in **/*.jar; do
      echo -n "[stripping] $file ..."
      strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" "$file"
      echo "done"
    done
   )
}

package() {
  local _size

  cd $_name-$pkgver
  # jar
  install -vDm 644 target/$_name.jar -t "$pkgdir/usr/share/java/$_pkgname/"
  # dependencies
  install -vDm 644 target/dependency/*.jar -t "$pkgdir/usr/share/java/$_pkgname/dependency/"
  # script
  install -vDm 755 ../$_pkgname -t "$pkgdir/usr/bin/"
  # XDG desktop file
  install -vDm 644 ../de.mediathekview.$_name.desktop -t "$pkgdir/usr/share/applications/"
  # icons
  for _size in 16 32 48 128; do
    install -vDm 644 target/${_name}@x$_size.png "$pkgdir/usr/share/icons/hicolor/${_size}x$_size/apps/$_pkgname.png"
  done
  install -vDm 644 res/$_name.svg -t "$pkgdir/usr/share/icons/hicolor/scalable/apps/$_pkgname.svg"
  # docs
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$_pkgname/"
}
