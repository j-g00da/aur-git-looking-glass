# $Id: PKGBUILD 174744 2016-05-11 04:23:20Z fyan $
# Maintainer: Brian Bidulock <bidulock@openss7.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Shanto <shanto@hotmail.com>
# Contributor: TDY <tdy@gmx.com>
# Contributor: zoe <chp321 [at] gmail [dot] com>
# Contributor: PhotonX <photon89 [at] gmail [dot] com>

pkgname=perl-goo-canvas
pkgver=0.06
pkgrel=13.6
pkgdesc="Perl bindings for GooCanvas"
arch=('i686' 'x86_64')
url="https://metacpan.org/pod/Goo::Canvas"
license=('GPL' 'PerlArtistic')
depends=('perl' 'cairo-perl' 'glib-perl' 'goocanvas1' 'gtk2-perl')
makedepends=('perl-extutils-depends' 'perl-extutils-pkgconfig')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/Y/YE/YEWENBIN/Goo-Canvas-$pkgver.tar.gz" privfunctions.patch)
md5sums=('7dfe0be8c17bfd641d18384d4fd8fb23'
         '55ad0e64de98546d101395ac246ed5b7')

prepare() {
  cd Goo-Canvas-$pkgver
  patch -Np2 -b -z .orig <../privfunctions.patch
}

build() {
  cd Goo-Canvas-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  PERL_USE_UNSAFE_INC=1 make
}

check() {
  cd Goo-Canvas-$pkgver
  make test
}

_perl_depends() {
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}

package() {
  cd Goo-Canvas-$pkgver
  make install DESTDIR="$pkgdir"
  _perl_depends
}
