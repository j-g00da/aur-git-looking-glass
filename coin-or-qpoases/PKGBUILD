# Maintainer: Guilhem Saurel <saurel@laas.fr>

pkgname=coin-or-qpoases
pkgver=3.2.2
pkgrel=1
pkgdesc="COIN-OR QP solver with online active set strategy"
arch=(i686 x86_64)
url="https://github.com/coin-or/qpOASES"
license=(LGPL2.1)
groups=(coin-or)
makedepends=(cmake)
depends=(blas lapack)
_name=qpOASES
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/releases/$pkgver.tar.gz")
sha256sums=('e36d795a17b067ea333793d96f17a14fb2bfbd92a4ab86c7f6f513cd9e3e640d')

prepare() {
  sed -i "s/STATIC/SHARED/" "qpOASES-releases-$pkgver/CMakeLists.txt"
}

build() {
  cmake -B "build-$pkgver" -S "qpOASES-releases-$pkgver" \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
      -Wno-dev
  cmake --build "build-$pkgver"
}

check() {
  "./build-$pkgver/bin/example1"
}

package() {
  DESTDIR="$pkgdir" cmake --build "build-$pkgver" -t install
}
