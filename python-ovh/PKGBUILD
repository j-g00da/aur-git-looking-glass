# Maintainer: Samuel-Zacharie Faure <samuel.faure.dev@gmail.com>
# Contributor: Mahdi Sarikhani <mahdisarikhani@outlook.com>
# Contributor: ebiadsu <ebiadsu@posteo.de>

pkgname=python-ovh
pkgver=1.1.2
pkgrel=2
pkgdesc="Lightweight wrapper around OVH's APIs"
arch=('any')
url="https://github.com/ovh/python-ovh"
license=('BSD')
depends=('python-requests')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-sphinx' 'python-wheel')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('b24a47bc37ffb14fee2d9525b4aa0b86eeb2aab24755fd6e74707c4e4d0b807a')

build() {
    cd "${pkgname}-${pkgver}"
    python -m build --wheel --no-isolation

    cd docs
    PYTHONPATH="..:${PYTHONPATH}" make man
}

package() {
    cd "${pkgname}-${pkgver}"
    python -m installer --destdir="${pkgdir}" dist/*.whl

    install -Dm644 docs/_build/man/python-ovh.1 "${pkgdir}/usr/share/man/man1/python-ovh.1"
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
