# Maintainer: Carlos Aznar√°n <caznaranl@uni.pe>
_base=pdbp
pkgname=python-${_base}
pkgdesc="A drop-in replacement for pdb and pdbpp"
pkgver=1.8.1
pkgrel=1
arch=(any)
url="https://github.com/mdmintz/${_base}"
license=(PSF-2.0)
depends=(python-pygments python-tabcompleter)
makedepends=(python-build python-installer python-setuptools python-wheel)
source=(${_base}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz)
sha512sums=('4078aae688a23d8269c3fd7f93e9856ad07155d93686a7ef0eebf9da849d6ad4856196ebf65c9683f5aa585184ffdac61ff1d2016abb599cecaff7ccf3cfced2')

build() {
  cd ${_base}-${pkgver}
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd ${_base}-${pkgver}
  PYTHONPYCACHEPREFIX="${PWD}/.cache/cpython/" python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
